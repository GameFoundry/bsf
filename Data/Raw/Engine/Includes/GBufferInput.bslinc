#include "$ENGINE$\SurfaceData.bslinc"

mixin GBufferInput
{
	mixin SurfaceData;
	mixin PerCameraData;

	code 
	{
		SamplerState gGBufferASamp;
		SamplerState gGBufferBSamp;
		SamplerState gGBufferCSamp;
		SamplerState gDepthBufferSamp;

		#if MSAA_COUNT > 1
		Texture2DMS<float4, MSAA_COUNT> gGBufferATex;
		Texture2DMS<float4, MSAA_COUNT>	gGBufferBTex;
		Texture2DMS<float2, MSAA_COUNT>	gGBufferCTex;
		Texture2DMS<float4, MSAA_COUNT> gDepthBufferTex;
		#else
		Texture2D gGBufferATex;
		Texture2D gGBufferBTex;
		Texture2D gGBufferCTex;
		Texture2D gDepthBufferTex;
		#endif
		
		SurfaceData decodeGBuffer(float4 GBufferAData, float4 GBufferBData, float2 GBufferCData, float deviceZ)
		{
			SurfaceData output;
			
			output.albedo.xyz = GBufferAData.xyz;
			output.albedo.w = 1.0f;
			output.worldNormal = GBufferBData * float4(2, 2, 2, 1) - float4(1, 1, 1, 0);
			output.worldNormal.xyz = normalize(output.worldNormal.xyz);
			output.depth = convertFromDeviceZ(deviceZ);
			output.roughness = GBufferCData.x;
			output.metalness = GBufferCData.y;
			
			return output;
		}
		
		#if MSAA_COUNT > 1
		SurfaceData getGBufferData(uint2 pixelPos, uint sampleIndex)
		{
			float4 GBufferAData = gGBufferATex.Load(pixelPos, sampleIndex);
			float4 GBufferBData = gGBufferBTex.Load(pixelPos, sampleIndex);
			float2 GBufferCData = gGBufferCTex.Load(pixelPos, sampleIndex).rg;
			float deviceZ = gDepthBufferTex.Load(pixelPos, sampleIndex).r;
			
			return decodeGBuffer(GBufferAData, GBufferBData, GBufferCData, deviceZ);
		}
		
		#else
		SurfaceData getGBufferData(uint2 pixelPos)
		{
			float4 GBufferAData = gGBufferATex.Load(int3(pixelPos, 0));
			float4 GBufferBData = gGBufferBTex.Load(int3(pixelPos, 0));
			float2 GBufferCData = gGBufferCTex.Load(int3(pixelPos, 0)).rg;
			float deviceZ = gDepthBufferTex.Load(int3(pixelPos, 0)).r;
			
			return decodeGBuffer(GBufferAData, GBufferBData, GBufferCData, deviceZ);
		}			
		#endif			
	};
};